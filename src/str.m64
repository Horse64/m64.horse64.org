## @module std
# Copyright (c) 2024, ellie/@ell1e & m64 stdlib authors (see AUTHORS.md).
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#
# 1. Redistributions of source code must retain the above copyright notice,
#    this list of conditions and the following disclaimer.
# 2. Redistributions in binary form must reproduce the above copyright notice,
#    this list of conditions and the following disclaimer in the documentation
#    and/or other materials provided with the distribution.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
# LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
# INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
# CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
# ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
# POSSIBILITY OF SUCH DAMAGE.
#
# Alternatively, at your option, this file is offered under the Apache 2
# license, see accompanied LICENSE.md.

c_import "stdlib.h" as c_stdlib
c_import "stdio.h" as c_stdlib
c_import "string.h" as c_stdlib

import limit from m64.horse64.org
import mem from m64.horse64.org

const str_stack_bytes <- size = 32

struct str {
    union {
        struct {
            var _stack_buf <- byte c_array[str_stack_bytes]
            var _stack_buf_len <- u16
        }
        struct {
            var _heap_buf <- byte array
        }
    }
    var _on_heap <- bool
}

func str.init_as_copy(
        other_str <- readonly str implicit ref
        ) failable override {
    self.copy_from(other_str)
    if failed(self.copy_from) {
        return failed
    }
}

func str.init_from_inline_c_str(
        c_str <- readonly byte c_array
        ) failable override {
    var c_array_var <- readonly byte array = mem.alloc_array(
        1, 0
    )
    if failed(mem.alloc_array) {
        return failed
    }
    c_array_var.data = c_str
    c_array_var.len = mem.c_str_nulpos(c_str)
    self.init_from_bytebuf(c_array_var)
    if failed(self.init_from_bytebuf) {
        return failed
    }
    c_array_var.data = none
    mem.unalloc_array(c_array_var)
}

func str.init_from_bytebuf(
        c_str <- readonly byte array
        ) failable override {
    var str_len <- size
    if c_str.len == mem.array_size_unset {
        str_len = mem.c_str_nulpos(c_str.data)
    } else {
        str_len = c_str.len
    }
    if str_len < str_stack_bytes {
        mem.memcpy(
            c_str.data, self._stack_buf, str_len
        )
        self._stack_buf[str_len + 1] = 0
        self._stack_buf_len = str_len + 1
        return
    }
    self._on_heap = yes
    if self._heap_buf != none {
        mem.unalloc_array(self._heap_buf)
    }
    self._heap_buf = mem.alloc_array(
        mem.size_of(self._heap_buf.data),
        str_len + 1
    )
    if failed(mem.alloc_array) {
        return failed
    }
    mem.memcpy(
        c_str.data, self._heap_buf.data, str_len
    )
    self._heap_buf[str_len + 1] = 0
    self._heap_buf.len = str_len + 1
}

func str.init failable {
    self._on_heap = no
    self._stack_buf[1] = 0
    self._stack_buf_len = 1
}

func str.add(other_str <- readonly str implicit ref) failable {
    var other_len <- size = other_str.len
    var other_data <- readonly byte c_array =
        other_str._stack_buf
    if other_str._on_heap {
        other_data = other_str._heap_buf.data
    }
    var other_array <- readonly byte array
    other_array.data = other_data
    other_array.len = other_len
    self.add(other_array)
    if failed(self.add) {
        return failed
    }
}

func str.add_bytebuf(
        c_str <- readonly byte array
        ) failable override {
    var c_str_len <- size = c_str.len
    if c_str_len == mem.array_size_unset {
        c_str_len = mem.c_str_nulpos(c_str.data)
    }
    var own_len <- size = self.len
    var needed_len <- size = c_str_len + own_len + 1
    if self._on_heap or needed_len > str_stack_bytes {
        var self_data <- byte c_array = self._stack_buf
        var clear_data <- byte c_array = none
        if self._on_heap {
            self_data = self._heap_buf.data
            clear_data = self._heap_buf.data
        }
        self._heap_buf.data = mem.alloc(needed_len)
        if failed(mem.alloc) {
            self._heap_buf.data = clear_data
            return failed
        }
        mem.memcpy(self_data, self._heap_buf.data, own_len)
        mem.memcpy(
            c_str.data, self._heap_buf.data + own_len, c_str_len
        )
        self._heap_buf[own_len + c_str_len + 1] = 0
        mem.unalloc(clear_data)
        self._heap_buf.len = needed_len
        return
    }
    mem.memcpy(
        c_str.data, self._stack_buf + own_len, c_str_len
    )
    self._stack_buf[own_len + c_str_len + 1] = 0
    self._stack_buf_len = needed_len
}

func str.len readonly -> size {
    if self._on_heap {
        return self._heap_buf.len - 1
    }
    return self._stack_buf_len - 1
}

func str.destroy {
    if self._on_heap {
        mem.unalloc_array(self._heap_buf)
    }
}

func str.copy_from(other_str <- readonly str implicit ref) failable {
    if not other_str._on_heap {
        if self._on_heap {
            mem.unalloc_array(self._heap_buf)
        }
        mem.memcpy(
            as_ref(other_str), as_ref(self),
            mem.size_of(self)
        )
        assert(not self._on_heap)
    } else {
        if self._on_heap {
            mem.unalloc_array(self._heap_buf)
        }
        self._on_heap = yes
        self._heap_buf = mem.alloc_array(
            1, other_str._heap_buf.len
        )
        if failed(mem.alloc_array) {
            return failed
        }
        mem.memcpy(
            other_str._heap_buf.data,
            self._heap_buf.data,
            other_str._heap_buf.len
        )
        self._heap_buf.len = other_str._heap_buf.len
    }
}

func str.get_c_str readonly -> readonly byte c_array {
    var result <- readonly byte c_array
    if self._on_heap {
        result = self._heap_buf.data
        return result
    }
    result = as(readonly byte c_array, self._stack_buf)
    return result
}

